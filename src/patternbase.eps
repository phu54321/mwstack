import locSetting;

object Pattern {
    var next: selftype;
    var timer;
    var x, y;
    var player;
    var forcePlayer;
    var callback: EUDFuncPtr(1, 1);
    var data;  // Custom data
};


const patternHead = Pattern.alloc();
var patternTail = patternHead;

function addPattern(player: TrgPlayer, x, y, callback: EUDFuncPtr(1, 1)) {
    const p = Pattern.alloc();
    p.callback = callback;
    p.timer = 0;
    p.x, p.y = x, y;
    p.player = player;
    p.forcePlayer = locSetting.forceFromPlayer(player);

    Pattern.cast(patternTail).next = p;
    patternTail = p;
    p.next = 0;
}


function loopPatterns() {
    var pv = patternHead.next;
    var pPrev = patternHead;
    while(pv) {
        const p = Pattern.cast(pv);

        locSetting.setMissileLocations(p.forcePlayer);

        const ret = p.callback(p);
        const pNext = p.next;

        if(!ret) {
            if(p == patternTail) patternTail = pPrev;
            Pattern.cast(pPrev).next = pNext;
            Pattern.free(p);
            pv = pNext;
        }
        else {
            p.timer += 1;
            pPrev = pv;
            pv = pNext;
        }
    }
}
