import locSetting;

object Pattern {
    var next: selftype;
    var timer;
    var x, y;
    var player;
    var forcePlayer;
    var callback: EUDFuncPtr(1, 1);
    var data;  // Custom data
};


var patternHead, patternTail = 0, 0;

function addPattern(player: TrgPlayer, x, y, callback: EUDFuncPtr(1, 1)) {
    const p = Pattern.alloc();
    p.callback = callback;
    p.timer = 0;
    p.x, p.y = x, y;
    p.player = player;
    p.forcePlayer = locSetting.forceFromPlayer(player);

    if(patternHead) {
        const lastPattern = Pattern.cast(patternTail);
        lastPattern.next = p;
        p.next = 0;
        patternTail = p;
    }

    else {
        p.next = 0;
        patternHead = p;
        patternTail = p;
    }
}

function loopPatterns() {
    if(!patternHead) return;

    var pv = patternHead;
    var pPrev = 0;
    while(pv) {
        const p = Pattern.cast(pv);
        const pNext = p.next;

        locSetting.setMissileLocations(p.forcePlayer);

        if(!p.callback(p)) {
            if(p == patternHead) {
                patternHead = pNext;
            }
            else {
                Pattern.cast(pPrev).next = pNext;
            }
            Pattern.free(p);
        }
        else {
            p.timer += 1;
        }
        pPrev = pv;
        pv = pNext;
    }
}
