import intro;
import location as loc;

const playerReviveTimer = EUDArray(6);
const reviveTime = 24 * 5;


function isPlayerAlive(p: TrgPlayer) {
    return playerexist(p) && Bring(p, AtLeast, 1, 'Builder', 'Anywhere');
}


// ----------------------------------------------------------------------------

function initLifeSys() {
    for(var player = 0 ; player < 6 ; player++) {
        if(playerexist(player)) {
            const playerForce = ($P1 <= player && player <= $P3) ? $Force1 : $Force2;
            SetScore(playerForce, Add, 1000, Custom);
        }
    }
    LeaderBoardScore(Custom, "\x16생존 점수");
    LeaderBoardComputerPlayers(Disable);
}


function lifeSysLoop() {
    // 리바이브 시스템
    //  - 가스 : 리바이브 카운터
    for(var player = 0 ; player < 6 ; player++) {
        if(!playerexist(player)) continue;
        const playerForce = ($P1 <= player && player <= $P3) ? $Force1 : $Force2;
        if(Bring(player, Exactly, 0, 'Builder', 'Anywhere')) {
            SetResources(player, Subtract, 5, Ore);
            SetResources(player, Subtract, 1, Gas);  // 리바이브 카운터 감소

            if(Accumulate(player, Exactly, 0, Gas)) {
                if(Score(playerForce, Custom, AtLeast, 200)) {
                    // 생존점수를 소모해 부활
                    SetScore(playerForce, Subtract, 200, Custom);
                    SetCurrentPlayer(player);
                    PlayWAV("staredit\\wav\\Notice.wav");
                    DisplayText("\x16부품 수리가 완료되었습니다. 부활후 3초간 무적상태를 유지합니다.");
                    CreateUnit(1, "Builder", playerForce == $Force1 ? $L('f1field') + 1 : $L('f2field') + 1, player);
                    SetResources(player, SetTo, 72, Gas);
                }
                else {
                    // 생존점수 부족 → 리바이브 불가.
                    PlayWAV("staredit\\wav\\Notice.wav");
                    DisplayText("\x16생존 점수가 부족하여 수리를 할 수 없습니다.");
                    SetResources(playerForce, SetTo, -1, Gas);
                }
            }
            else if(Accumulate(player, AtLeast, -10, Gas)) {
                // 가스를 -1로 유지.
                SetResources(player, SetTo, -1, Gas);
            }
        }
        else {
            const prevOre = dwread_epd(EPD(0x57F0F0) + player);
            if(Accumulate(player, Exactly, 1, Gas)) {
                SetCurrentPlayer(player);
                PlayWAV("staredit\\wav\\Notice.wav");
                DisplayText("\x16무적 타이머가 끝났습니다.");
            }
            SetResources(player, Subtract, 1, Gas);  // 무적타이머
            const multiplier = Score(playerForce, Custom, Exactly, 0) ? 2 : 1;
            SetResources(player, Add, multiplier * (10 - prevOre / 200), Ore);  // 돈벌자~
            if(Accumulate(player, AtLeast, 2000, Ore)) {
                SetResources(player, SetTo, 2000, Ore);
            }
        }
    }

    SetInvincibility(Enable, '(any unit)', AllPlayers, 'Anywhere');
}
